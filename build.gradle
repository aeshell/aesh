
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'application'
apply plugin: 'checkstyle'

group = 'org.jboss.aesh'
version = '0.55'

sourceCompatibility = 1.7
targetCompatibility = 1.7

jar {
    manifest {
        attributes 'Implementation-Title': 'Aesh', 'Implementation-Version': version
    }
}

test {
    testLogging.showStandardStreams = true
    //maxParallelForks = 1
    forkEvery = 1
    onOutput { descriptor, event ->
    logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
  }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.fusesource.jansi', name: 'jansi', version: '1.11'
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

mainClassName = "Example"

idea {
    project {
        jdkName = "1.7"
        ipr {
            withXml { provider ->
                provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
                def maxHeapSizeConfig =  provider.node.component.find { it.@name == 'JavacSettings' }
                if( maxHeapSizeConfig == null ){
                    def javacSettingsNode =  provider.node.appendNode('component',[name: 'JavacSettings'])
                    javacSettingsNode.appendNode('option', [name:"MAXIMUM_HEAP_SIZE", value:"512"])
                }
            }
            beforeMerged { project ->
                project.modulePaths.clear()
            }
        }
    }
    module {
        name = "Ã¦sh"
    }
}

